if(TARGET onnxruntime_interface)
else()
	set(ONNX_RUNTIME_PATH "$ENV{ONNX_RUNTIME_PATH}" CACHE PATH "Where to find ONNX runtime")
	if("${ONNX_RUNTIME_PATH}" STREQUAL "")
		message(FATAL_ERROR "Please specify cmake variable ONNX_RUNTIME_PATH! E.g. via -DONNX_RUNTIME_PATH=/path/to/onnxruntime")
	endif()
	set(TRT_RUNTIME_PATH "$ENV{TRT_RUNTIME_PATH}" CACHE PATH "Where to find TensorRT")
	if("${TRT_RUNTIME_PATH}" STREQUAL "")
		message(WARNING "Please specify cmake variable TRTRTX_RUNTIME_PATH! E.g. via -DTRT_RUNTIME_PATH=/path/to/tensorrt. This will ensure all libraries are copied to the execution directory.")
	endif()
	
	find_library(ONNXRUNTIME_LIB onnxruntime HINTS ${ONNX_RUNTIME_PATH} ${ONNX_RUNTIME_PATH}/lib REQUIRED)
	find_library(ONNXRUNTIME_PROVIDERS_SHARED_LIB onnxruntime_providers_shared HINTS ${ONNX_RUNTIME_PATH} ${ONNX_RUNTIME_PATH}/lib REQUIRED)
	find_library(ONNXRUNTIME_CUDA_EP_LIB onnxruntime_providers_cuda HINTS ${ONNX_RUNTIME_PATH} ${ONNX_RUNTIME_PATH}/lib)
	if(WIN32)
		find_file(ONNXRUNTIME_DLL onnxruntime.dll HINTS ${ONNX_RUNTIME_PATH} ${ONNX_RUNTIME_PATH}/bin REQUIRED)
		find_file(ONNXRUNTIME_PROVIDERS_SHARED_DLL onnxruntime_providers_shared.dll HINTS ${ONNX_RUNTIME_PATH} ${ONNX_RUNTIME_PATH}/bin REQUIRED)
		find_file(ONNXRUNTIME_CUDA_EP_DLL onnxruntime_providers_cuda.dll HINTS ${ONNX_RUNTIME_PATH} ${ONNX_RUNTIME_PATH}/lib)
		find_file(ONNXRUNTIME_TRT_EP_DLL onnxruntime_providers_tensorrt.dll HINTS ${ONNX_RUNTIME_PATH} ${ONNX_RUNTIME_PATH}/lib)
		find_file(TRT_DLL nvinfer_10.dll HINTS ${TRT_RUNTIME_PATH} ${TRT_RUNTIME_PATH}/bin)
		find_file(TRT_PLUGIN_DLL nvinfer_plugin_10.dll HINTS ${TRT_RUNTIME_PATH} ${TRT_RUNTIME_PATH}/bin)
		find_file(TRT_ONNXPARSER_DLL nvonnxparser_10.dll HINTS ${TRT_RUNTIME_PATH} ${TRT_RUNTIME_PATH}/bin)

	endif()

	find_path(ONNXRUNTIME_INCLUDE onnxruntime/onnxruntime_cxx_api.h HINTS ${ONNX_RUNTIME_PATH}/include REQUIRED)
	add_library(onnxruntime_interface INTERFACE)
	target_include_directories(onnxruntime_interface SYSTEM INTERFACE ${ONNXRUNTIME_INCLUDE})
	target_link_libraries(onnxruntime_interface INTERFACE ${ONNXRUNTIME_LIB} ${ONNXRUNTIME_PROVIDERS_SHARED_LIB})

	message(STATUS "ONNX runtime include \"${ONNXRUNTIME_INCLUDE}\"")
	message(STATUS "ONNX runtime lib \"${ONNXRUNTIME_LIB}\"")
endif()

set(RUNTIME_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR})

macro(copy_file_to_bin_dir file)
	get_property(is_multi_config GLOBAL PROPERTY GENERATOR_IS_MULTI_CONFIG)
	if(${is_multi_config})
		foreach(config IN LISTS CMAKE_CONFIGURATION_TYPES)
			set(OUTPUT_DIR ${RUNTIME_DIRECTORY}/${config})
			execute_process(COMMAND ${CMAKE_COMMAND} -E make_directory "${OUTPUT_DIR}")
			configure_file(${file} "${OUTPUT_DIR}" COPYONLY)
		endforeach()
	else()
		configure_file(${file} "${RUNTIME_DIRECTORY}" COPYONLY)
	endif()
endmacro()


if(WIN32)
	copy_file_to_bin_dir(${ONNXRUNTIME_DLL})
	copy_file_to_bin_dir(${ONNXRUNTIME_PROVIDERS_SHARED_DLL})
	copy_file_to_bin_dir(${ONNXRUNTIME_TRT_EP_DLL})
	copy_file_to_bin_dir(${TRT_DLL})
	copy_file_to_bin_dir(${TRT_PLUGIN_DLL})
	copy_file_to_bin_dir(${TRT_ONNXPARSER_DLL})
	copy_file_to_bin_dir(${ONNXRUNTIME_CUDA_EP_DLL})
endif()